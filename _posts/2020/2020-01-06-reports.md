---
layout: post
title:	"Reports"
date:	2020-01-06 12:00:00
categories:
    - blog
tags:
    - sap
    - reports
---

A sample project in `report`.

##### 1 main program, calling other 2 report programs

~~~abap
1) REPORT z15081947_mini.
2) REPORT  z15081947_mini_reports_1.
3) REPORT z15081947_mini_reports_2.
~~~

#### REPORT z15081947_mini.
~~~abap
*Flight Booking Application
*
*There are two options in selection screen
*
*Block Title - Search
*
*1)Flight Information
*
*2)Customer ID
*
*On Choosing the first radio button, a selection block
*
*with the following fields must appear
*
*CARRID, CONNID , FLDATE
*
*On choosing the second radio button,
*
*CUSTOMER ID
*
*Report1 based on searching by Flight Information.
*
*---------------------------------------------------
*
*Fields
*
*-------
*
*Carrier Name | City FROM | City TO | Date | Passenger Name(Hot spot On ) | LUGGWEIGHT | WUNIT | LOCCURAM
*
*if the  LUGGWEIGHT is more than 10, the field should be highlighted in RED Colour, otherwise in GREEN.
*
*The report should be displayed in a tabular format with proper list headings.
*
*Total Weight must be displayed underneath the table .
*
*On clicking on the passenger name, the user must to taken to a secondary list that displays the following fields
*
*NAME  | FORM | STREET | POSTBOX | POSTCODE | CITY | COUNTRY
*
*Use a Function module to pick the above data. The FM will have the customer
*
*ID as importing parameter and the other field must be exported .
*
*Report2 based on searching by Customer ID.
*
*---------------------------------------------------
*
*Page Heading - > Customer Name: ** / Customer ID: ** / Date of Birth: ** / Email ID: **
*
*Fields
*
*-------
*
*Booking Info
*
*------------
*
*Carrier name | Connid | fldate | Booking ID | Price(LOCCURAM)
*
*Sum ( Sub-Total using COLLECT Statement) cost spent on each carrier.
*
*At the end of the table, display the grand total of the cost.

REPORT z15081947_mini.

* Radio button block
* HELP: https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abapparameters_screen.htm#!ABAP_ADDITION_5@5@
SELECTION-SCREEN BEGIN OF BLOCK search_block WITH FRAME TITLE text-001.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(20) flt_info.
PARAMETERS: rad_flt RADIOBUTTON GROUP rgb USER-COMMAND any_ucomm DEFAULT 'X'.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(20) cus_id.
PARAMETERS: rad_cus RADIOBUTTON GROUP rgb.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK search_block.

*
* Goto -> Text Elements -> Selection Texts.
*

* Selection screen 1
SELECTION-SCREEN BEGIN OF BLOCK flight_block WITH FRAME TITLE text-002.
PARAMETERS: carrid TYPE sbook-carrid MODIF ID sc1.
PARAMETERS: connid TYPE sbook-connid MODIF ID sc1.
PARAMETERS: fldate TYPE sbook-fldate MODIF ID sc1.
SELECTION-SCREEN END OF BLOCK flight_block.

*Selection screen 2
SELECTION-SCREEN BEGIN OF BLOCK customid_block WITH FRAME TITLE text-003.
PARAMETERS: customid TYPE sbook-customid MODIF ID sc2.
SELECTION-SCREEN END OF BLOCK customid_block.

AT SELECTION-SCREEN OUTPUT.
*  Toggle the selection screens based on radio buttons
  LOOP AT SCREEN.
    IF rad_flt = 'X' AND screen-group1 = 'SC2'.
      PERFORM modify_screen. "Calling subroutine
    ELSEIF rad_cus = 'X' AND screen-group1 = 'SC1'.
      PERFORM modify_screen. "Calling subroutine
    ENDIF.
  ENDLOOP.


*&
*& https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/index.htm?file=abenreporting_process.htm
*&
START-OF-SELECTION.
  CASE 'X'.
    WHEN rad_flt.
      IF carrid IS NOT INITIAL AND connid IS NOT INITIAL AND fldate IS NOT INITIAL.
        SUBMIT z15081947_mini_reports_1
*          VIA SELECTION-SCREEN
          WITH p_car EQ carrid
          WITH p_con EQ connid
          WITH p_fl  EQ fldate
        AND RETURN.
      ENDIF.
    WHEN rad_cus.
      IF customid IS NOT INITIAL.
        SUBMIT z15081947_mini_reports_2
        WITH p_id EQ customid
        AND RETURN.
      ENDIF.
  ENDCASE.

INITIALIZATION.
  flt_info = 'Flight Information'.
  cus_id   = 'Customer ID'.

* Subroutines.
FORM modify_screen.
  screen-active = 0.
  MODIFY SCREEN.
ENDFORM.
~~~

#### REPORT  z15081947_mini_reports_1.

~~~abap
*Report1 based on searching by Flight Information.
*
*---------------------------------------------------
*
*Fields
*
*-------
*
*Carrier Name | City FROM | City TO | Date | Passenger Name(Hot spot On ) | LUGGWEIGHT | WUNIT | LOCCURAM
*
*if the  LUGGWEIGHT is more than 10, the field should be highlighted in RED Colour, otherwise in GREEN.
*
*The report should be displayed in a tabular format with proper list headings.
*
*Total Weight must be displayed underneath the table .
*
*On clicking on the passenger name, the user must to taken to a secondary list that displays the following fields
*
*NAME  | FORM | STREET | POSTBOX | POSTCODE | CITY | COUNTRY
*
*Use a Function module to pick the above data. The FM will have the customer
*
*ID as importing parameter and the other field must be exported .
*

REPORT  z15081947_mini_reports_1.

*
* Join Tables SBOOK, SPFLI, SCARR.
* Use SCUSTOM for passenger details.
*

* These parameters needs to be passed from the main program 'Z*_MINI'
PARAMETERS: p_car TYPE sbook-carrid,
            p_con TYPE sbook-connid,
            p_fl  TYPE sbook-fldate.

* Final Structure of the internal table 'it_final' and workarea 'wa_final'.
TYPES: BEGIN OF struct_final,
         carrid     TYPE sbook-carrid,
         connid     TYPE sbook-connid,

         customid   TYPE sbook-customid, " can be used to map 'ID' field in SCUSTOM.

         fldate     TYPE sbook-fldate,
         passname   TYPE sbook-passname,
         luggweight TYPE sbook-luggweight,
         wunit      TYPE sbook-wunit,
         loccuram   TYPE sbook-loccuram,

         cityfrom   TYPE spfli-cityfrom,
         cityto     TYPE spfli-cityto,

         carrname   TYPE scarr-carrname,
       END OF struct_final.

* Structure of internal table 'it_spfli' and workarea 'wa_spfli'.
TYPES: BEGIN OF struct_spfli,
         carrid   TYPE spfli-carrid,
         connid   TYPE spfli-connid,

         cityfrom TYPE spfli-cityfrom,
         cityto   TYPE spfli-cityto,
       END OF struct_spfli.

* Structure of internal table 'it_scarr' and workarea 'wa_scarr'
TYPES: BEGIN OF struct_scarr,
         carrid   TYPE scarr-carrid,
         carrname TYPE scarr-carrname,
       END OF struct_scarr.

* Structure of internal table 'it_scustom' and workarea 'wa_scustom'
* NAME  | FORM | STREET | POSTBOX | POSTCODE | CITY | COUNTRY
TYPES: BEGIN OF struct_scustom,
         id       TYPE scustom-id,
         name     TYPE scustom-name,
         form     TYPE scustom-form,
         street   TYPE scustom-street,
         postbox  TYPE scustom-postbox,
         postcode TYPE scustom-postcode,
         city     TYPE scustom-city,
         country  TYPE scustom-country,
       END OF struct_scustom.

* Internal table 'it_final' and workarea 'wa_final' with structure 'struct_final'
DATA: it_final TYPE TABLE OF struct_final WITH KEY fldate passname luggweight,
      wa_final LIKE LINE  OF it_final.

* Internal table 'it_spfli' and workarea 'wa_spfli' with structure 'struct_spfli'
DATA: it_spfli TYPE TABLE OF struct_spfli,
      wa_spfli LIKE LINE OF it_spfli.

* Internal table 'it_scarr' and workarea 'wa_scarr' with structure 'struct_scarr'
DATA: it_scarr TYPE TABLE OF struct_scarr,
      wa_scarr LIKE LINE OF it_scarr.

* Internal table 'it_scustom' and workarea 'wa_scustom' with structure 'struct_scustom'
*DATA: it_scustom TYPE TABLE OF struct_scustom,
*      wa_scustom LIKE LINE OF it_scustom.
DATA: wa_scustom TYPE struct_scustom.

START-OF-SELECTION.
* Fetch carrid, connid, fldate from SBOOK.
  SELECT carrid connid customid fldate passname luggweight wunit loccuram
    FROM sbook
    INTO CORRESPONDING FIELDS OF TABLE it_final
    WHERE carrid = p_car   AND
          connid = p_con   AND
          fldate = p_fl.

* Fetch cityfrom, cityto from SPFLI.
  SELECT carrid connid cityfrom cityto
    FROM spfli
    INTO CORRESPONDING FIELDS OF TABLE it_spfli
    WHERE carrid = p_car AND
          connid = p_con.

*  Fetch carrid, carrname from SCARR
  SELECT carrid carrname
    FROM scarr
    INTO CORRESPONDING FIELDS OF TABLE it_scarr
    WHERE carrid = p_car.

* Joining internal tables 'it_final', 'it_spfli' and 'it_scarr'
  LOOP AT it_final INTO wa_final.
    AT FIRST.
      WRITE: / 'Carrier Name', 'City FROM', 'City TO', 'Date', 'Passenger Name', 'LUGGWEIGHT', 'WUNIT', 'LOCCURAM'.
      ULINE.
    ENDAT.

    READ TABLE it_spfli INTO wa_spfli WITH KEY carrid = wa_final-carrid connid = wa_final-connid.
    READ TABLE it_scarr INTO wa_scarr WITH KEY carrid = wa_final-carrid.

*  'cityfrom' and 'cityto' from SPFLI corresponding to 'carrid' and 'connid' from SBOOK.
    wa_final-cityfrom = wa_spfli-cityfrom.
    wa_final-cityto   = wa_spfli-cityto.

*    'carrname' from SCARR corresponding to 'carrid' from SBOOK
    wa_final-carrname = wa_scarr-carrname.

*  Modify the internal table 'it_final' with new data.
    MODIFY it_final FROM wa_final.

*    if the  LUGGWEIGHT is more than 10, the field should be highlighted in RED Colour, otherwise in GREEN.
    IF wa_final-luggweight GT 10.
      WRITE: / wa_final-carrname COLOR 2, wa_final-cityfrom, wa_final-cityto, wa_final-fldate COLOR 2, wa_final-passname HOTSPOT, wa_final-luggweight COLOR 6, wa_final-wunit, wa_final-loccuram.
    ELSE.
      WRITE: / wa_final-carrname COLOR 2, wa_final-cityfrom, wa_final-cityto, wa_final-fldate COLOR 2, wa_final-passname HOTSPOT, wa_final-luggweight COLOR 5, wa_final-wunit, wa_final-loccuram.
    ENDIF.

*  Total Weight
    AT LAST.
      SUM.
      ULINE.
      WRITE: /50 'Total Weight: ', wa_final-luggweight.
    ENDAT.

    HIDE: wa_final-customid. " Store the 'customid' globally so that when user selects line, corresponding 'customid' is stored globally.
  ENDLOOP.

* On clicking on the passenger name, the user must to taken to a secondary list that displays the following fields
* NAME  | FORM | STREET | POSTBOX | POSTCODE | CITY | COUNTRY
* ie Fields from SCUSTOM.

AT LINE-SELECTION.

**  For debugging!
*  WRITE: / 'Selected Line'.
*  WRITE: / sy-lisel.
*  WRITE: / 'The corresponding customid: ', wa_final-customid.
*  ULINE.

*  Fetch data from SCUSTOM based on 'customid'
*  Use a Function module to pick the above data. The FM will have the customer
*  ID as importing parameter and the other field must be exported .
  CALL FUNCTION 'Z15081947_MINI_SCUSTOM_FETCH'
    EXPORTING
      id         = wa_final-customid
    IMPORTING
      wa_scustom = wa_scustom.


* Writing the corresponding data
  WRITE: / 'Name', 'Form', 'Street', 'Postbox', 'Postcode', 'City', 'Country'.
  ULINE.
  WRITE: / wa_scustom-name, wa_scustom-form, wa_scustom-street, wa_scustom-postbox, wa_scustom-postcode, wa_scustom-city, wa_scustom-country.
~~~

#### REPORT z15081947_mini_reports_2.
~~~abap
*Report2 based on searching by Customer ID.
*
*---------------------------------------------------
*
*Page Heading - > Customer Name: ** / Customer ID: ** / Date of Birth: ** / Email ID: **
*
*Fields
*
*-------
*
*Booking Info
*
*------------
*
*Carrier name | Connid | fldate | Booking ID | Price(LOCCURAM)
*
*Sum ( Sub-Total using COLLECT Statement) cost spent on each carrier.
*
*At the end of the table, display the grand total of the cost.

REPORT z15081947_mini_reports_2.

PARAMETERS: p_id TYPE scustom-id.

* Structure for TOP-OF-PAGE
TYPES: BEGIN OF struct_scustom,
         id    TYPE scustom-id,
         name  TYPE scustom-name,
         email TYPE scustom-email,
       END OF struct_scustom.

* Structure for Book Info
TYPES: BEGIN OF struct_final,
         carrid   TYPE sbook-carrid, " Key for SBOOK and SCARR.

         connid   TYPE sbook-connid,
         fldate   TYPE sbook-fldate,
         bookid   TYPE sbook-bookid,
         loccuram TYPE sbook-loccuram,

         customid TYPE sbook-customid, " Key for fetch into 'it_final'

         carrname TYPE scarr-carrname,
       END OF struct_final.

* Structure for SCARR
TYPES: BEGIN OF struct_scarr,
         carrid   TYPE scarr-carrid,
         carrname TYPE scarr-carrname,
       END OF struct_scarr.

* Structure for collect
TYPES: BEGIN OF struct_collect,
         carrid   TYPE sbook-carrid,
         loccuram TYPE sbook-loccuram,
       END OF struct_collect.

* Top of page.
DATA: wa_scustom TYPE struct_scustom.

* Book Info internal table and workarea - final
DATA: it_final TYPE TABLE OF struct_final,
      wa_final LIKE LINE OF it_final.

* Book Info internal table and workarea - scarr
DATA: it_scarr TYPE TABLE OF struct_scarr,
      wa_scarr LIKE LINE OF it_scarr.

* Internal table and workarea for collect.
DATA: it_collect TYPE TABLE OF struct_collect,
      wa_collect LIKE LINE OF it_collect.

TOP-OF-PAGE.
  WRITE: / 'NAME: ', wa_scustom-name COLOR 4, 'ID: ', wa_scustom-id COLOR 4,'EMAIL: ', wa_scustom-email COLOR 4.
  ULINE.

START-OF-SELECTION.
* Fectching data from SCUSTOM
  SELECT SINGLE id name
    FROM scustom
    INTO CORRESPONDING FIELDS OF wa_scustom
    WHERE id = p_id.

*  Fetching data from SBOOK
  SELECT carrid connid fldate bookid loccuram customid
    FROM sbook
    INTO CORRESPONDING FIELDS OF TABLE it_final
    WHERE customid = p_id.

* Fetchning data from SCARR
  SELECT carrid carrname
    FROM scarr
    INTO CORRESPONDING FIELDS OF TABLE it_scarr.

* Joining SCARR and SBOOK.
  LOOP AT it_final INTO wa_final.
    READ TABLE it_scarr INTO wa_scarr WITH KEY carrid = wa_final-carrid.

    wa_final-carrname = wa_scarr-carrname.

    MODIFY it_final FROM wa_final.
  ENDLOOP.

* Sorting internal table 'it_final' based on carrid.
*  SORT it_final BY carrid.

* Creating the collect internal table 'it_collect'
  LOOP AT it_final INTO wa_final.
    wa_collect-carrid = wa_final-carrid.
    wa_collect-loccuram = wa_final-loccuram.
    COLLECT wa_collect INTO it_collect.
  ENDLOOP.

* Looping at 'it_final' to print the output in below format.
*
*Carrier name | Connid | fldate | Booking ID | Price(LOCCURAM)
*Sum ( Sub-Total using COLLECT Statement) cost spent on each carrier.
*At the end of the table, display the grand total of the cost.
  LOOP AT it_final INTO wa_final.
*    AT FIRST.
*      WRITE: / 'Carrier Name', 'Connid', 'Fldate', 'Booking ID', 'Price(loccuram)'.
*    ENDAT.

*   Print details
    WRITE: / 'CARRIER NAME: ', wa_final-carrname COLOR 5,
             'CONNID: ',       wa_final-connid   COLOR 5,
             'DATE: ',         wa_final-fldate   COLOR 5,
             'PRICE: ',        wa_final-loccuram COLOR 5.

*   Read internal table 'it_collect' and get the subtotal 'loccuram' based on 'carrid'
    READ TABLE it_collect INTO wa_collect WITH KEY carrid = wa_final-carrid.
*   Now 'wa_collect' has loccuram and carrid.

*   At the end of each carrid, print the subtotal
    AT END OF carrid.
      WRITE: /59 'PRICE(SUBTOTAL): ', wa_collect-loccuram COLOR 1.
      ULINE.
    ENDAT.

    AT LAST.
      SUM.
      WRITE: /69 'TOTAL: ', wa_final-loccuram  COLOR 6.
    ENDAT.

  ENDLOOP.
~~~